list:
    @just --list

build:
    cargo build

lint:
    @cargo clippy --version
    cargo clippy -- -D warnings

test:
    cargo test --lib

integration-test:
    cargo test --test integration_test

build-integration-tests:
    cargo test --test integration_test --no-run --features reqwest_provider

export ANDROID_HOME := env_var_or_default("ANDROID_HOME", "~/Library/Android/sdk")
export HOST_ARCH := arch()

install-ndk:
    #!/usr/bin/env bash
    SDK_MANAGER=${ANDROID_HOME}/cmdline-tools/latest/bin/sdkmanager
    NDK="ndk;28.1.13356709"

    echo "Installing ndk: $NDK"
    $SDK_MANAGER --install $NDK

install-ios-targets:
    #!/usr/bin/env bash
    rustup target add aarch64-apple-ios-sim aarch64-apple-ios aarch64-apple-ios-macabi aarch64-apple-darwin
    if [ ${HOST_ARCH} == "x86_64" ]; then \
        rustup target add x86_64-apple-ios; \
    fi

install-android-targets:
    rustup target add x86_64-linux-android aarch64-linux-android armv7-linux-androideabi
    cargo install cargo-ndk@3.5.4

test-ios:
    #!/usr/bin/env bash
    set -o pipefail
    IOS_VERSION=$(xcrun simctl list devices available | grep -E '^-- iOS' | head -1 | sed 's/-- iOS \([0-9.]*\) --.*/\1/')
    DEVICE_ID=$(xcrun simctl list devices available | grep iPhone | head -1 | sed 's/.*(\([A-F0-9-]*\)).*/\1/')
    echo "Using iOS $IOS_VERSION Simulator: $DEVICE_ID"
    xcodebuild -project tests/ios/GemTest/GemTest.xcodeproj \
    -scheme GemTest \
    -sdk iphonesimulator \
    -destination "platform=iOS Simulator,OS=$IOS_VERSION,id=$DEVICE_ID" \
    test | xcbeautify

export LIB_NAME := "gemstone"
export FW_NAME := "Gemstone"
export STATIC_LIB_NAME := "lib" + LIB_NAME + ".a"
export DY_LIB_NAME := if os() == "macos" { "libgemstone.dylib" } else { "libgemstone.so" }

export BUILD_MODE_ENV := env_var_or_default("BUILD_MODE", "debug")
export BUILD_MODE_TARGET := if BUILD_MODE_ENV == "release" { "release" } else { "debug" }
export BUILD_FLAG := if BUILD_MODE_TARGET == "release" { "--release" } else { "" }

export TARGET_DIR := "../target"
export GEN_SWIFT_FOLDER := "generated/swift"
export GEN_KOTLIN_FOLDER := "generated/kotlin"
export ANDROID_MODULE_FOLDER := "android/gemstone/src/main/java"

export TARGET_XC_FW_FOLDER := "target/spm"
export FW_FFI_NAME := FW_NAME + "FFI"
export FW_FFI_FILE := FW_FFI_NAME + ".framework"
export XC_FW_FFI_NAME := FW_FFI_NAME + ".xcframework"
export DEPLOYMENT_TARGET := env_var_or_default("IPHONEOS_DEPLOYMENT_TARGET", "17.0")
build-ios: build-targets bindgen-swift assemble-frameworks xcframework cp-xcframework-source

build-targets:
    #!/usr/bin/env bash
    echo "Host: ${HOST_ARCH}, iOS deployment target ${DEPLOYMENT_TARGET}, BUILD_MODE_TARGET: ${BUILD_MODE_TARGET}"
    if [ ${HOST_ARCH} == "x86_64" ]; then \
        IPHONEOS_DEPLOYMENT_TARGET=${DEPLOYMENT_TARGET} cargo build --timings --target x86_64-apple-ios ${BUILD_FLAG}; \
    else \
        IPHONEOS_DEPLOYMENT_TARGET=${DEPLOYMENT_TARGET} cargo build --timings --target aarch64-apple-ios-sim --target aarch64-apple-ios --target aarch64-apple-ios-macabi --target aarch64-apple-darwin ${BUILD_FLAG}; \
    fi

bindgen-swift:
    #!/usr/bin/env bash
    echo "Bindgen swift $STATIC_LIB_NAME"
    mkdir -p ${GEN_SWIFT_FOLDER}
    if [ "${BUILD_MODE_TARGET}" == "release" ]; then \
        export RUSTFLAGS="${RUSTFLAGS:-} -C strip=none"; \
    fi
    cargo build ${BUILD_FLAG}
    LIB_PATH=$(find "${TARGET_DIR}" -type f -name "${DY_LIB_NAME}" -path "*/${BUILD_MODE_TARGET}/${DY_LIB_NAME}" | head -n 1); \
    if [ -z "${LIB_PATH}" ]; then \
        echo "Failed to locate ${DY_LIB_NAME} in ${TARGET_DIR} (mode ${BUILD_MODE_TARGET})"; \
        exit 1; \
    fi; \
    cargo run ${BUILD_FLAG} -p uniffi-bindgen -- generate --library --language swift --crate ${LIB_NAME} "${LIB_PATH}" -o ${GEN_SWIFT_FOLDER}

assemble-frameworks:
    #!/usr/bin/env bash
    MODULE_MAP=$(
    cat << EOF
    // This file was autogenerated by some hot garbage in the uniffi crate.
    framework module ${FW_FFI_NAME} {
        header "${FW_FFI_NAME}.h"
        export *
    }
    EOF
    )
    cd ${TARGET_DIR} && find . -type d -name ${FW_FFI_FILE} | xargs rm -rf
    targets=(aarch64-apple-ios-sim aarch64-apple-ios aarch64-apple-ios-macabi aarch64-apple-darwin)
    if [ ${HOST_ARCH} == "x86_64" ]; then
        targets=(x86_64-apple-ios)
    fi
    for target in ${targets[@]}; do \
        pushd ${TARGET_DIR}/${target}/${BUILD_MODE_TARGET} > /dev/null && \
        mkdir -p ${FW_FFI_FILE} && cd ${FW_FFI_FILE} && mkdir Headers Modules && echo "$MODULE_MAP" > ./Modules/module.modulemap && cp ../../../../${LIB_NAME}/${GEN_SWIFT_FOLDER}/${FW_FFI_NAME}.h ./Headers && cp ../${STATIC_LIB_NAME} ./${FW_FFI_NAME} && cp ../../../../${LIB_NAME}/src/Info.plist ./; \
        popd > /dev/null; \
    done

xcframework:
    #!/usr/bin/env bash
    rm -rf ${TARGET_XC_FW_FOLDER}/Sources/${XC_FW_FFI_NAME} && mkdir -p ${TARGET_XC_FW_FOLDER}/Sources
    if [ ${HOST_ARCH} == "x86_64" ]; then \
        xcodebuild -create-xcframework -framework ${TARGET_DIR}/x86_64-apple-ios/${BUILD_MODE_TARGET}/${FW_FFI_FILE} -output ${TARGET_XC_FW_FOLDER}/Sources/${XC_FW_FFI_NAME}; \
    else \
        xcodebuild -create-xcframework -framework ${TARGET_DIR}/aarch64-apple-ios/${BUILD_MODE_TARGET}/${FW_FFI_FILE} -framework ${TARGET_DIR}/aarch64-apple-ios-sim/${BUILD_MODE_TARGET}/${FW_FFI_FILE} -framework ${TARGET_DIR}/aarch64-apple-ios-macabi/${BUILD_MODE_TARGET}/${FW_FFI_FILE} -framework ${TARGET_DIR}/aarch64-apple-darwin/${BUILD_MODE_TARGET}/${FW_FFI_FILE} -output ${TARGET_XC_FW_FOLDER}/Sources/${XC_FW_FFI_NAME}; \
    fi

cp-xcframework-source:
    #!/usr/bin/env bash
    mkdir -p ${TARGET_XC_FW_FOLDER}/Sources/${FW_NAME}
    cp src/Package.swift ${TARGET_XC_FW_FOLDER}
    cp ${GEN_SWIFT_FOLDER}/${FW_NAME}.swift ${TARGET_XC_FW_FOLDER}/Sources/${FW_NAME}

bindgen-kotlin:
    #!/usr/bin/env bash
    echo "Bindgen kotlin BUILD_MODE_TARGET: ${BUILD_MODE_TARGET}"
    rm -rf ${GEN_KOTLIN_FOLDER} ${ANDROID_MODULE_FOLDER}/uniffi
    mkdir -p ${GEN_KOTLIN_FOLDER} ${ANDROID_MODULE_FOLDER}
    cargo ndk-env
    if [ "${BUILD_MODE_TARGET}" == "release" ]; then \
        export RUSTFLAGS="${RUSTFLAGS:-} -C strip=none"; \
    fi
    cargo build ${BUILD_FLAG}
    LIB_PATH=$(find "${TARGET_DIR}" -type f -name "${DY_LIB_NAME}" -path "*/${BUILD_MODE_TARGET}/${DY_LIB_NAME}" | head -n 1); \
    if [ -z "${LIB_PATH}" ]; then \
        echo "Failed to locate ${DY_LIB_NAME} in ${TARGET_DIR} (mode ${BUILD_MODE_TARGET})"; \
        exit 1; \
    fi; \
    cargo run ${BUILD_FLAG} -p uniffi-bindgen -- generate --library --language kotlin --crate ${LIB_NAME} "${LIB_PATH}" -o ${GEN_KOTLIN_FOLDER}
    cp -Rf ${GEN_KOTLIN_FOLDER}/uniffi ${ANDROID_MODULE_FOLDER}

build-android: bindgen-kotlin
    #!/usr/bin/env bash
    rm -rf ~/.m2/repository/com/gemwallet/gemstone/gemstone
    cd android && touch local.properties && ./gradlew publishToMavenLocal

publish-android:
    #!/usr/bin/env bash
    cd android && touch local.properties && ./gradlew publishReleasePublicationToGPRRepository
